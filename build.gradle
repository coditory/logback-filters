plugins {
    id 'java-library'
    id 'groovy'
    id 'jacoco'
    id 'signing'
    id 'com.github.kt3k.coveralls' version '2.10.2'
    id 'com.coditory.build' version '0.1.7'
    id 'io.codearte.nexus-staging' version '0.22.0' apply false
    id 'de.marcphilipp.nexus-publish' version '0.4.0' apply false
}

group = 'com.coditory.common'
description = 'Coditory Logback Filters'

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    api 'ch.qos.logback:logback-classic:1.2.3'
    testImplementation 'org.spockframework:spock-core:2.0-M3-groovy-3.0'
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

javadoc {
    source = sourceSets.main.allJava
    classpath = configurations.compileClasspath
    failOnError = false
    options {
        memberLevel = JavadocMemberLevel.PUBLIC
        author = true
    }
}

tasks.withType(JavaCompile) {
    // make all test/main builds use UTF-8
    options.encoding = 'UTF-8'
    // compile only changed files
    options.incremental = true
    // skip JsonCreator from Jackson
    options.compilerArgs << "-parameters"
    // error instead of warn on lint problems
    options.compilerArgs << "-Werror"
    options.compilerArgs << "-Xlint"
    options.compilerArgs << "-Xlint:-serial"
    options.compilerArgs << "-Xlint:-processing"
    options.compilerArgs << "-Xlint:-try"
}

tasks.withType(Test) {
    useJUnitPlatform()
}

if (project.hasProperty('publish')) {
    apply from: "gradle/publish.gradle"
}